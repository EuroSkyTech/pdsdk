src := "native/pdsdk/target"
src-bindings := "native/pdsdk/target/bindings/ios"
lib := "libpdsdk.a"

build: link
    swift build

test: build
    swift test

xcode:
    open Package.swift

clean:
    just clean-target aarch64-apple-darwin
    just clean-target aarch64-apple-ios
    just clean-target aarch64-apple-ios-sim
    rm -rf .build/
    rm -rf .swiftpm/

clean-target target:
    rm -rf PdSdkFramework.xcframework/{{target}}

link:
    just link-target aarch64-apple-darwin
    just link-target aarch64-apple-ios
    just link-target aarch64-apple-ios-sim

link-target target:
    mkdir -p PdSdkFramework.xcframework/{{target}}/headers/PdSdkFramework
    cd PdSdkFramework.xcframework/{{target}}/headers/PdSdkFramework/ && \
        ln -fsw ../../../../../../{{src-bindings}}/pdsdkFFI.modulemap module.modulemap && \
        ln -fsw ../../../../../../{{src-bindings}}/pdsdkFFI.h pdsdkFFI.h
    cd PdSdkFramework.xcframework/{{target}} && ln -fsw ../../../../{{src}}/{{target}}/release/{{lib}} {{lib}}
    cd Sources/PdSdk/ && ln -fsw ../../../../{{src-bindings}}/pdsdk.swift pdsdk.swift
    